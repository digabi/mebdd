<head>
<!--
	MEBDD
	
	Copyright 2014 Matti Lattu and Matriculation Examination Board of Finland
	
	This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <http://www.gnu.org/licenses/>.
	
-->
<title>MEB DD 1.3c</title>
<HTA:APPLICATION 
     APPLICATIONNAME="MEB DD"
     SCROLL="yes"
     SINGLEINSTANCE="yes"
     WINDOWSTATE="maximize"
	 ICON="digabi_kuukkeli.ico"
>
	<style type="text/css">
		p { font-family: Arial; font-size: 12pt; }
		h1 { font-family: Arial; font-size: 16pt; }
		ul { font-family: Arial; font-size: 12pt; }
		.radio_disk_normal { font-family: Courier; color: black; }
		.radio_disk_warning { font-family: Courier; color: red; }
		.file_path { font-family: Courier; }
		.header { font-family: Arial; font-size: 130%; color: white; }
		.cellIcon { vertical-align: top; width: 100px; }
		.cellText { vertical-align: top; }
		
		.imageTableNormal { font-family: Arial; font-size: 12pt; background-color: white; }
		.imageTableSelected { font-family: Arial; font-size: 12pt; background-color: grey; }
		
		.progressLayer { position: absolute; width: 200px; height: 200px; z-index: 2; left: 200px; top: 200px; margin-left: auto; margin-right: auto; background-color: #ffffff; border-style: solid; border-color: black; font-family: Arial; padding: 20px; }
	</style>
</head>

<script language="VBScript" src="mebdd_windows.vbs"></script>
<script language="VBScript" src="mebdd_backend.vbs"></script>

<script language="VBScript">
	Option Explicit
	
	Const REG_INSTALL_PATH = "SOFTWARE\MEB Finland\mebdd"
	Const REG_INSTALL_KEY = "path_mebdd"
	Const REG_HKCU_PATH = "Software\MEB Finland\mebdd"
	Const REG_LAST_TAG_KEY = "LastTag"
	Const LOCALDATA_PATH = "MEB Finland\mebdd"
	Const IMAGE_SOURCE_LEGEND = 0
	Const IMAGE_SOURCE_DESCRIPTION = 1
	Const IMAGE_SOURCE_URL_IMAGE = 2
	Const IMAGE_SOURCE_URL_MD5 = 3
	Const IMAGE_SOURCE_LOCAL_PATH = 4
	Const IMAGE_SOURCE_MD5_REG_PATH = 5
	Const IMAGE_SOURCE_MD5_REMOTE_REG_KEY = 6
	Const IMAGE_SOURCE_MD5_LOCAL_REG_KEY = 7
	
	' NOTE! NOTE! We have to give the FULL PATH for these command-line tools.
	' Please make sure you change these in case you relocate the utilities.
	Dim BIN_CURL, BIN_MEBDD_WORKER, BIN_MEBMD5, BIN_UNZIP
	
	BIN_CURL = Win_GetMEBDDInstallationPath() & "\curl.exe"
	BIN_MEBDD_WORKER = Win_GetMEBDDInstallationPath() & "\mebdd_worker.exe"
	BIN_MEBMD5 = Win_GetMEBDDInstallationPath() & "\mebmd5.exe"
	BIN_UNZIP = Win_GetMEBDDInstallationPath() & "\unzip.exe"
	
	' Data for Image Sources
	'  - tag names should contain only alphanumeric characters (a-z, A-Z, 0-9)
	'  - tag 'local_image' is reserved for locally selected image file
	'  - tag 'local_zero' is reserved for local zero image
	
	Dim IMAGE_SOURCE
	Set IMAGE_SOURCE = CreateObject("Scripting.Dictionary")
	' KTP & KOE
	IMAGE_SOURCE.Add "digabiktp", Array("Digabi KTP", "Digabin koetilan palvelin (KTP)", "http://kurko.digabi.fi/latest/ktp.zip", "http://kurko.digabi.fi/latest/ktp.zip.md5", Win_GetLocalImageDirectoryPath() & "ktp.dd", REG_HKCU_PATH, "MD5_digabiktp_remote", "MD5_digabiktp_local")
	IMAGE_SOURCE.Add "digabikoe", Array("Digabi KOE", "Digabin kokelaan päätelaite (KOE)", "http://kurko.digabi.fi/latest/koe.zip", "http://kurko.digabi.fi/latest/koe.zip.md5", Win_GetLocalImageDirectoryPath() & "koe.dd", REG_HKCU_PATH, "MD5_digabikoe_remote", "MD5_digabikoe_local")
	' DigabiOS demo
	IMAGE_SOURCE.Add "digabi64", Array("DigabiOS 64bit", "DigabiOS-demokäyttöjärjestelmä, 64 bittinen", "https://digabi.fi/64bit", "https://digabi.fi/64bit/md5", Win_GetLocalImageDirectoryPath() & "DigabiOS-64bit.dd", REG_HKCU_PATH, "MD5_digabi64_remote", "MD5_digabi64_local")
	IMAGE_SOURCE.Add "digabi32", Array("DigabiOS 32bit", "DigabiOS-demokäyttöjärjestelmä, 32 bittinen", "https://digabi.fi/32bit", "https://digabi.fi/32bit/md5", Win_GetLocalImageDirectoryPath() & "DigabiOS-32bit.dd", REG_HKCU_PATH, "MD5_digabi32_remote", "MD5_digabi32_local")
	' Local tests
	'IMAGE_SOURCE.Add "digabiktplocal", Array("Digabi KTP", "Digabin koetilan palvelin (KTP), paikallinen", "http://127.0.0.1:8080/ktp.zip", "http://127.0.0.1:8080/ktp.zip.md5", Win_GetLocalImageDirectoryPath() & "ktp.dd", REG_HKCU_PATH, "MD5_digabiktp_remote_localhost", "MD5_digabiktp_local_localhost")
	'IMAGE_SOURCE.Add "digabikoelocal", Array("Digabi KOE", "Digabin kokelaan päätelaite (KOE), paikallinen", "http://127.0.0.1:8080/koe.zip", "http://127.0.0.1:8080/koe.zip.md5", Win_GetLocalImageDirectoryPath() & "koe.dd", REG_HKCU_PATH, "MD5_digabikoe_remote_localhost", "MD5_digabikoe_local_localhost")
	' Do not change these
	IMAGE_SOURCE.Add "local_zero", Array("Tyhjennä", "Valitse tämä, kun haluat tyhjentää USB-muistit", "", "", Win_GetMEBDDInstallationPath() & "\zero_image.iso", "", "", "")
	IMAGE_SOURCE.Add "local_image", Array("Paikallinen tiedosto (.iso, .dd)", "<input type=""button"" id=""buttonOpenFile"" value=""Valitse tiedosto"" onClick=""AskImageFilePath()"">", "", "", "", "", "", "")
	
	Dim MEBDD_LOG_FILE_PATH
	MEBDD_LOG_FILE_PATH = Win_GetEnvironmentString("TEMP") & "\mebdd.txt"
	
	Dim strCurrentImageTag
	strCurrentImageTag = ""

	Dim objTimerOnload, objTimerScandisks
	

	' Append given message to MEBDD_LOG_FILE_PATH
	Sub LogMessage (strMessage)
		Dim objFSO, objFile
		
		Set objFSO = CreateObject("Scripting.FileSystemObject")
		' Open file for appending (8)
		Set objFile = objFSO.OpenTextFile(MEBDD_LOG_FILE_PATH, 8, True)
		objFile.WriteLine(Date & " " & Time & " : " & strMessage)
		objFile.Close
	End Sub
	
	' Retrieve all remote MD5 digests and store them to HKCU registry
	Sub GetRemoteVersions
		Dim strThisTag, strThisDigest
		
		For Each strThisTag in IMAGE_SOURCE
			If IMAGE_SOURCE(strThisTag)(IMAGE_SOURCE_URL_MD5) <> "" Then
				LogMessage("Retrieving MD5 digest from " & IMAGE_SOURCE(strThisTag)(IMAGE_SOURCE_URL_MD5))
				strThisDigest = BEnd_DownloadMD5(IMAGE_SOURCE(strThisTag)(IMAGE_SOURCE_URL_MD5))
				If strThisDigest = "" Then
					' We have an error
					LogMessage("Error while retrieving MD5 digest from " & IMAGE_SOURCE(strThisTag)(IMAGE_SOURCE_URL_MD5) & ": " & BEnd_LastError)
				End If
				
				If not Win_WriteRegKeyHKCU(REG_HKCU_PATH, IMAGE_SOURCE(strThisTag)(IMAGE_SOURCE_MD5_REMOTE_REG_KEY), strThisDigest) Then
					LogMessage("Failed to write to registry")
				End If
			End If
		Next
	End Sub

	' Populates image selection table from IMAGE_SOURCE (hash array) to
	' objImageSelectionTable (HTML object)
	Sub PopulateImages
		Dim strThisTag, strThisRow, strTable, strLocalDigest, strRemoteDigest
		
		strTable = "<table>"
		
		For Each strThisTag in IMAGE_SOURCE
			strThisRow = "<tr id=""objImageTableRow_" & strThisTag & """ class=""imageTableNormal"">"
			strThisRow = strThisRow & "<td>" & IMAGE_SOURCE(strThisTag)(IMAGE_SOURCE_LEGEND) & "</td>"
			strThisRow = strThisRow & "<td>" & IMAGE_SOURCE(strThisTag)(IMAGE_SOURCE_DESCRIPTION) & "</td>"
			
			strLocalDigest = Win_ReadRegKeyHKCU(IMAGE_SOURCE(strThisTag)(IMAGE_SOURCE_MD5_REG_PATH), IMAGE_SOURCE(strThisTag)(IMAGE_SOURCE_MD5_LOCAL_REG_KEY))
			strRemoteDigest = Win_ReadRegKeyHKCU(IMAGE_SOURCE(strThisTag)(IMAGE_SOURCE_MD5_REG_PATH), IMAGE_SOURCE(strThisTag)(IMAGE_SOURCE_MD5_REMOTE_REG_KEY))
			
			' Create action button
			If (not Win_FileExists(IMAGE_SOURCE(strThisTag)(IMAGE_SOURCE_LOCAL_PATH)) and strThisTag <> "local_image") Then
			
				' The image file does not exist. Offer download.
				
				strThisRow = strThisRow & "<td><input type=""button""" & _
					" id=""buttonSelect_" & strThisTag & """" & _
					" onClick=""Download('" & strThisTag & "')"" value=""Lataa""></td>"
			ElseIf (IMAGE_SOURCE(strThisTag)(IMAGE_SOURCE_MD5_REMOTE_REG_KEY) <> "" and strLocalDigest <> strRemoteDigest) Then
				
				' Remote image has different digest than local. Offer update (download).
				
				strThisRow = strThisRow & "<td><input type=""button""" & _
					" id=""buttonSelect_" & strThisTag & """" & _
					" onClick=""Download('" & strThisTag & "')"" value=""Päivitä""></td>"
			Else
			
				' We have a good local image file. Offer select.
				
				strThisRow = strThisRow & "<td><input type=""button""" & _
					" id=""buttonSelect_" & strThisTag & """" & _
					" onClick=""SelectImage('" & strThisTag & "')"" value=""Valitse""></td>"
			End If
			
			strThisRow = strThisRow & "</tr>"
				
			strTable = strTable & strThisRow
		Next
		
		strTable = strTable & "</table>"

		objImageSelectionTable.InnerHTML = strTable
	End Sub
	
	Sub PopulateDrives
		Dim n, arrSelectedDrives
		n = 0
		
		' Check selected drives before scanning new drives
		arrSelectedDrives = GetSelectedDrives()
		
		' Clear write status icon
		SetWriteStatus(0)
		
		' Reset radio buttons
		objRadioButtonArea.InnerHTML = ""
		
		Dim objWMIService, colDiskDrives, objDiskDrive, strRadioHTML, strRadioItemHTML, strRadioClass, strRadioChecked
		
		Set objWMIService = GetObject("winmgmts:{impersonationLevel=impersonate}!\\.\root\cimv2")
		'Set colDiskDrives = objWMIService.ExecQuery ("Select * from Win32_DiskDrive Where MediaType=""Removable Media""")
		Set colDiskDrives = objWMIService.ExecQuery ("Select * from Win32_DiskDrive Where MediaType Like ""Removable Media%""")
		
		strRadioHTML = ""

		For each objDiskDrive in colDiskDrives
			n = n + 1
			
			' Pre-check drives which were already selected
			strRadioChecked = ""
			If BEnd_InArray(objDiskDrive.DeviceID, arrSelectedDrives) Then
				strRadioChecked = " checked"
			End If
			

			' Render suspicious drives with "radio_disk_warning" class instead of "radio_disk_normal"
			strRadioClass = "radio_disk_normal"
			If objDiskDrive.Size > 17179869184 Then
				' This is too big to a USB thumb drive (bigger than 16 Gb)
				strRadioClass = "radio_disk_warning"
			End If
			
			strRadioItemHTML = "<span class=" & strRadioClass & "><input type=checkbox name=formDisk_" & n & " value=" & Chr(34) & objDiskDrive.DeviceID & Chr(34) & " onclick=""CheckSelectedDrive"" " & strRadioChecked & ">" & objDiskDrive.Model & ", " & objDiskDrive.Caption & " (" & BEnd_ConvertSize(objDiskDrive.Size) & ")</span><br/>"
			
			strRadioHTML = strRadioHTML & strRadioItemHTML
		Next
		
		objRadioButtonArea.InnerHTML = strRadioHTML & "<input type=hidden name=formDisk_length value=" & n & ">"
		
		If (n = 0) Then
			objRadioButtonArea.InnerHTML = objRadioButtonArea.InnerHTML & "<p>Varoitus: USB-muisteja ei löytynyt. Liitä yksi tai useampi USB-muisti tietokoneeseen.</p>"
		End If
		
		CheckSelectedDrive
		
	End Sub
	
	Sub ButtonsDisable
		'buttonWriteImage.disabled = True
		'buttonWriteZeroImage.disabled = True
		'buttonPopulateDrives.disabled = True
		'buttonDownload64.disabled = True
		'buttonDownload32.disabled = True
		'buttonOpenFile.disabled = True
		' formImageName.disabled = True
	End Sub
	
	Sub ButtonsEnable
		'buttonWriteImage.disabled = False
		'buttonWriteZeroImage.disabled = False
		'buttonPopulateDrives.disabled = False
		'buttonDownload64.disabled = False
		'buttonDownload32.disabled = False
		'buttonOpenFile.disabled = False	
		' formImageName.disabled = False
	End Sub
	
	Function GetSelectedDrives ()
		Dim n, nn, intDriveLength, arrSelectedDrives()
		
		ReDim arrSelectedDrives(0)
		nn=0
		
		If not IsObject(formDisk_length) Then
			' The disk selection menu does not exist
			GetSelectedDrives = arrSelectedDrives
			Exit Function
		End If
		
		intDriveLength = formDisk_length.Value

		' Loop through all checkboxes
		For n = 1 to intDriveLength
			If Eval("formDisk_" & n & ".Checked") Then
				nn = nn + 1
				ReDim Preserve arrSelectedDrives(nn)
				arrSelectedDrives(nn) = Eval("formDisk_" & n & ".value")
			End If
			SetWriteStatus(0)
		Next
		
		GetSelectedDrives = arrSelectedDrives
	End Function
	
	' Downloads given [strImageTag]. Returns TRUE on success, FALSE on failure.
	' If the IMAGE_SOURCE_URL_IMAGE of the given [strImageTag] is empty, does not download and returns SUCCESS
	' If the IMAGE_SOURCE_URL_MD5 of the given [strImageTag] is empty, does not verify MD5
	Function Download (strImageTag)
		Dim boolResult, boolZIP, strLocalFile
		
		' Quit if image URL is empty and return Success
		If IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_URL_IMAGE) = "" Then
			Download = True
			Exit Function
		End If
		
		' Make sure that strImageTag exists
		If IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_LEGEND) = "" Then
			LogMessage "Tried to download image for tag '" & strImageTag & "' but there is no such image tag"
			Download = False
			Exit Function
		End If
		
		If IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_URL_MD5) <> "" Then
			' Download MD5 sum
			ButtonsDisable
			ProgressEnable("Ladataan tarkistussumma...")
			LogMessage "Downloading MD5 sum"
			LogMessage "MD5-summan lataus käynnistetty (" & IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_LEGEND) & ")"
			If BEnd_DownloadMD5AndStore( _
				IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_URL_MD5), _
				IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_MD5_REG_PATH), _
				IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_MD5_LOCAL_REG_KEY)) Then
				' Success
				LogMessage "Succeeded in downloading MD5 sum"
			Else
				' Fail
				ProgressDisable
				AppendWriteStatus("MD5-summan lataus epäonnistui, virhekoodi: " & BEnd_LastError)
				Download = False
				Exit Function
			End If
			ProgressDisable
		End If
		
		' Are we downloading a zip?
		If UCase(Win_GetFileExtension(IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_URL_IMAGE))) = "ZIP" Then
			' Create a temporary filename for the zip file
			strLocalFile = Win_GetTempFilename()
			boolZIP = True
			LogMessage "Created temporary file for ZIP: " & strImageTag
		Else
			strLocalFile = IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_LOCAL_PATH)
			boolZIP = False
		End If
		
		' Download image file
		ProgressEnable("Ladataan levynkuva. Tämä kestää kauan...")
		boolResult = BEnd_DownloadImage(IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_URL_IMAGE), strLocalFile)
		ProgressDisable
		
		If boolResult Then
			AppendWriteStatus("Levynkuvan lataus onnistui")

			If IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_URL_MD5) <> "" Then
				' The settings contain URL for the MD5 so we check the file integrity
				
				ProgressEnable("Tarkistetaan ladattua levynkuvaa...")
		
				' Verify file MD5 to downloaded MD5
				If BEnd_FileMD5IsEqual(strLocalFile, Win_ReadRegKeyHKCU(IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_MD5_REG_PATH), IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_MD5_LOCAL_REG_KEY))) Then
					' Success
					AppendWriteStatus("Ladattu levynkuva on virheetön.")
					
					If boolZIP Then
						ProgressEnable("Puran ladatun levynkuvan...")

						If not BEnd_Unbox("zip", strLocalFile, IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_LOCAL_PATH)) Then
							ProgressDisable
							AppendWriteStatus("Levynkuvan purkaminen epäonnistui.")
							LogMessage("Image unboxing failed, error: " & BEnd_LastError)
							MsgBox "Ladatun levynkuvan purkaminen epäonnistui. Lataa tiedosto uudelleen.", 16, "Virhe"
							
							' Delete temporary files
							If Win_FileExists(IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_LOCAL_PATH)) and not Win_DeleteFile(IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_LOCAL_PATH)) Then
								LogMessage("Warning: Could not remove failed image file " & IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_LOCAL_PATH))
							End If
							
							If Win_FileExists(strLocalFile) and not Win_DeleteFile(strLocalFile) Then
								LogMessage("Warning: Could not remove local temporary zip file " & strLocalFile)
							End If

							Download = False
							Exit Function
						End If
						
						If Win_FileExists(strLocalFile) and not Win_DeleteFile(strLocalFile) Then
							LogMessage("Warning: Could not remove local temporary zip file " & strLocalFile)
						End If
						
						ProgressDisable
						AppendWriteStatus("Ladattu levynkuva on purettu")
					End If
					
					ProgressDisable
					Download = True
				Else
					' Fail
					AppendWriteStatus("Levynkuvan tarkistus epäonnistui.")
					LogMessage("Image verify failed, error: " & BEnd_LastError)
					MsgBox "Ladatussa levynkuvassa on virheitä. Lataa tiedosto uudelleen.", 16, "Virhe"
					' Delete failed file & hash
					boolResult = Win_DeleteFile(IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_LOCAL_PATH))
					boolResult = Win_WriteRegKeyHKCU(IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_MD5_REG_PATH), IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_MD5_LOCAL_REG_KEY), "")
					
					ProgressDisable
					Download = False
					Exit Function
				End If

				ProgressDisable
			Else
				' There was no URL for the MD5 so we expect that the file is ok
				Download = True
			End If
		Else
			ProgressDisable
			LogMessage("Image download failed, error: " & BEnd_LastError)
			AppendWriteStatus("Levynkuvan lataus epäonnistui.")
			MsgBox "Levynkuvan lataus epäonnistui.", 16, "Virhe"

			' Delete failed file & hash
			boolResult = Win_DeleteFile(IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_LOCAL_PATH))
			boolResult = Win_WriteRegKeyHKCU(IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_MD5_REG_PATH), IMAGE_SOURCE(strImageTag)(IMAGE_SOURCE_MD5_LOCAL_REG_KEY), "")

			Download = False
			Exit Function
		End If
		
		ProgressDisable
		
		' Now we have successfully downloaded image
		' Refresh the UI
		PopulateImages
	End Function
	
    Sub AskImageFilePath
		Dim strPath
		
		' Clear write status icon
		SetWriteStatus(0)
	
		strPath = Win_DialogImageFile("Valitse tiedosto", Win_GetLocalImageDirectoryPath(), "*.iso;*.dd")
		
		If strPath <> "" Then
			' Set file path IMAGE_SOURCE properties
			If Win_FileExists(strPath) Then
				' Good, the file even exists
				IMAGE_SOURCE("local_image")(IMAGE_SOURCE_LOCAL_PATH) = strPath
			End If
		End If
	End Sub
	
	' Sets currently selected image as [strImageTag]
	Sub SelectImage (strImageTag)
		Dim objRow, strThisTag
		
		strCurrentImageTag = strImageTag
		
		' Update UI (objImageTable)
		For Each strThisTag in IMAGE_SOURCE
			Set objRow = document.getElementById("objImageTableRow_" & strThisTag)
			If strThisTag = strImageTag Then
				' This is the selected row
				objRow.className = "imageTableSelected"
			Else
				' This is normal row
				objRow.className = "imageTableNormal"
			End If
		Next
	End Sub
	
	' Checks that current image tag (strCurrentImageTag) points to an existing file name
	Sub CheckImagePath
		If (Win_FileExists(IMAGE_SOURCE(strCurrentImageTag)(IMAGE_SOURCE_LOCAL_PATH))) Then
			objStep1Logo.InnerHTML = "<img src=""marssiva_kukko_harmaa.png"">"
		Else
			objStep1Logo.InnerHTML = "<img src=""marssiva_kukko_vihrea.png"">"
		End If
	End Sub
	
	' Writes strCurrentImageTag image to the selected drives
	Sub WriteImage
		Dim boolWriteSuccess, strImageFile, arrSelectedDrives, boolVerifyImage, intReturnCode
		
		' Silently exit if strCurrentImageTag has not been set
		If strCurrentImageTag = "" Then
			LogMessage "Cowardly refusing to write the image as current image tag is not set"
			Exit Sub
		End If
		
		' Get image path
		strImageFile = IMAGE_SOURCE(strCurrentImageTag)(IMAGE_SOURCE_LOCAL_PATH)
		
		' Get selected drives
		arrSelectedDrives = GetSelectedDrives()
		
		If (UBound(arrSelectedDrives) = 0) Then
			MsgBox "Valitse USB-muisti", 16, "Virhe"
			Exit Sub
		End If

		intReturnCode = MsgBox("Varoitus! Tämä toimenpide poistaa kaikki tiedostot valitsemiltasi USB-muisteilta. Oletko varma, että haluat jatkaa?", 1+32+256, "Vahvista")
		
		If intReturnCode <> 1 Then
			MsgBox "Toimenpide peruutettiin", 64, "Peruutus"
			Exit Sub
		End If
		
		If checkboxVerify.checked Then
			' Execute mebdd_worker with verify
			boolVerifyImage = True
		Else
			' No verify this time
			boolVerifyImage = False
		End If
		
		If strCurrentImageTag = "local_zero" Then
			AppendWriteStatus("Levynkuvan kirjoitus (USB-muistin tyhjennys) on käynnistetty.")
			ProgressEnable("Tyhjennän USB-muistin/muisteja...")
		Else
			' Store last image path
			If not Win_WriteLastImageTag(strCurrentImageTag) Then
				LogMessage("Could not store last selected image: " & Win_LastError)
			End If
			ProgressEnable("Kirjoitan USB-muistin/muistit...")
		End If

		boolWriteSuccess = BEnd_WriteImage(strImageFile, arrSelectedDrives, boolVerifyImage)
		
		ProgressDisable
		
		If boolWriteSuccess Then
			' Write succeeded
			SetWriteStatus(1)
			If strCurrentImageTag = "local_zero" Then
				AppendWriteStatus("Levynkuvan kirjoitus (USB-muistin tyhjennys) onnistui")
				MsgBox "USB-muisti on tyhjennetty. Irrota ja liitä muisti takaisin. Hyväksy ehdotus alustuksesta (formatoinnista).", 64
			Else
				AppendWriteStatus("Levynkuvan kirjoitus onnistui")
			End If
		Else
			' Write Failed
			SetWriteStatus(-1)

			If BEnd_LastError = "IMAGE_FILE_NOT_FOUND" Then
				MsgBox "Tiedostoa " & strImageFile & " ei löydy", 16, "Virhe"
			ElseIf BEnd_LastError = "NO_DRIVES_SELECTED" Then
				MsgBox "Valitse USB-muisti", 16, "Virhe"
			Else
				AppendWriteStatus("Levynkuvan kirjoitus epäonnistui, virhekoodi " & BEnd_LastError & _
				" <a href='file://" & Win_GetEnvironmentString("TEMP") & "\mebdd.txt'>Avaa lokitiedosto</a>")
			End If
		End If
	End Sub
	
	Sub CheckSelectedDrive
		Dim arrSelectedDrives, objButton
		
		arrSelectedDrives = GetSelectedDrives()
		
		If (UBound(arrSelectedDrives) > 0) Then
			objStep2Logo.InnerHTML = "<img src=""marssiva_kukko_vihrea.png"">"
		Else
			objStep2Logo.InnerHTML = "<img src=""marssiva_kukko_harmaa.png"">"
		End If
	End Sub
	
	Sub SetWriteStatus (intStatus)
		' Status = 0 : grey logo (neutral)
		' Status < 0 : red logo (failed)
		' Status > 0 : green logo (success)
		
		If intStatus = 0 Then
			objStep3Logo.InnerHTML = "<img src=""marssiva_kukko_harmaa.png"">"
		End If
		
		If intStatus < 0 Then
			objStep3Logo.InnerHTML = "<img src=""marssiva_kukko_punainen.png"">"
		End If
		
		If intStatus > 0 Then
			objStep3Logo.InnerHTML = "<img src=""marssiva_kukko_vihrea.png"">"
		End If
	End Sub
	
	Function AppendWriteStatus (strNewMessage)
		objWriteStatus.InnerHTML = Time & " : " & strNewMessage & "<br/>" & objWriteStatus.InnerHTML
		
		' Append to log as well
		LogMessage strNewMessage
		
		' Return always TRUE (Why this is not a sub?)
		AppendWriteStatus = True
	End Function
	
	' Make progress layer visible with text [strMessage]
	Sub ProgressEnable (strMessage) 
		document.getElementById("objProgressLayerText").InnerHTML = strMessage
		document.getElementById("objProgressLayer").style.visibility = "visible"
		AppendWriteStatus(strMessage)
		ButtonsDisable
	End Sub
	
	' Make progress layer invisible
	Sub ProgressDisable
		document.getElementById("objProgressLayer").style.visibility = "hidden"
		ButtonsEnable
	End Sub
	
	Sub Window_onLoad_Delayed
		window.clearInterval(objTimerOnload)
		
		' Enable progress layer
		ProgressEnable("Tarkistan uusimpia versioita palvelimelta...")
		
		' Refresh remote versions
		GetRemoteVersions
		
		' Create image selections
		PopulateImages
		
		' Select stored image tag
		SelectImage(strCurrentImageTag)
		
		' Hide progress layer
		ProgressDisable
		
		' Set timer to rescan disks every now and then
		objTimerScandisks = window.setInterval("PopulateDrives", 5000)
		
	End Sub
	
	' Magically named Window_onLoad is executed when HTA page is loaded
	Sub Window_onLoad
		Dim strLastImageTag
		
		' Execute time-consuming init things after the screen has been drawn
		objTimerOnload = window.setInterval("Window_onLoad_Delayed", 1000)
		
		' Set drive options
		PopulateDrives
		
		' Get last stored image tag and set is as current image
		strLastImageTag = Win_ReadLastImageTag()
		
		If strLastImageTag <> "" Then
			If IMAGE_SOURCE(strLastImageTag)(IMAGE_SOURCE_LEGEND) <> "" Then
				strCurrentImageTag = Win_ReadLastImageTag()
			End If
		End If
		
		' Set write status to neutral
		SetWriteStatus(0)
	End Sub

	' This is executed whenever user exits the script.
	Sub Window_OnExit
		' Clear timer intervals
		window.clearInterval(objTimerOnload)
		window.clearInterval(objTimerScandisks)
		' Log exit
		LogMessage "Exiting by user"
		
	End Sub
	
</script>

<body onBeforeUnload="Window_OnExit">
<table>
<tr><td colspan="2"><h1>Vaihe 1: Millaisen USB-muistin haluat?</h1></td></tr>
<tr>
	<td id="objStep1Logo" class="cellIcon"></td>
	<td class="cellText">
		<span id="objImageSelectionTable"></span>
	</td>
</tr>

<tr><td colspan="2"><h1>Vaihe 2: Valitse USB-muistit</h1></td></tr>
<tr>
	<td id="objStep2Logo" class="cellIcon"></td>
	<td class="cellText">
	<p>Liitä USB-muistit tietokoneeseen ja valitse kirjoitettavat USB-muistit alla olevasta listasta:</p>
	<span class="radio_disk_normal">
	<span id="objRadioButtonArea"><input type="hidden" name="formDisk_length" value="0"></span>
	</td>
</tr>

<tr><td colspan="2"><h1>Vaihe 3: Kirjoita tiedosto USB-muistille</h1></td></tr>
<tr>
	<td id="objStep3Logo" class="cellIcon"></td>
	<td class="cellText">
	<p><input type="checkbox" id="checkboxVerify" name="checkboxVerify" checked>Tarkista kirjoitettu USB-muisti kirjoitusvirheiden varalta</p>
	<p>Kun olet valinnut kopioitavan tiedoston ja käytettävän USB-muistin, napsauta allaolevasta painikkeesta.</p>
	<input type="button" id="buttonWriteImage" value="Käynnistä USB-muistin kirjoitus" onclick="WriteImage()">
	<br/><br/>
	<span id="objWriteStatus"></span>
	</td>
</tr>
</table>

<div id="objProgressLayer" class="progressLayer" style="visibility: hidden">
	<img style="margin: 10px" src="working.gif"/><br/>
	<span id="objProgressLayerText"></span>
</div>

<!-- This is needed for file open dialog -->
<OBJECT ID=Dlg CLASSID="CLSID:3050F4E1-98B5-11CF-BB82-00AA00BDCE0B" width=0 height=0> </OBJECT>
</body>
